// Copyright (C) 2009-2023, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki mutator DEPTH_FAIL_VISUALIZATION 0 1

#include <AnKi/Shaders/Common.hlsl>
#include <AnKi/Shaders/Include/GpuSceneTypes.h>

struct Uniforms
{
	Vec4 m_color;
	Mat4 m_viewProjMat;
};

[[vk::push_constant]] ConstantBuffer<Uniforms> g_unis;

[[vk::binding(2)]] StructuredBuffer<GpuSceneRenderableBoundingVolume> g_renderableBoundingVolumes;
[[vk::binding(3)]] StructuredBuffer<U32> g_visibleRenderableBoundingVolumeIndices;

struct VertOut
{
	Vec4 m_svPosition : SV_POSITION;
};

#pragma anki technique_start vert
struct VertIn
{
	[[vk::location(0)]] Vec3 m_position : POSITION;
	U32 m_svInstanceId : SV_INSTANCEID;
};

VertOut main(VertIn input)
{
	VertOut output;

	const U32 bvolumeCount = g_visibleRenderableBoundingVolumeIndices[0];

	if(input.m_svInstanceId < bvolumeCount)
	{
		const GpuSceneRenderableBoundingVolume bvol = g_renderableBoundingVolumes[g_visibleRenderableBoundingVolumeIndices[input.m_svInstanceId + 1]];
		const Vec3 boxCenter = (bvol.m_aabbMin + bvol.m_aabbMax) * 0.5f;
		Vec3 localPos = input.m_position * (bvol.m_aabbMax - boxCenter) + boxCenter;
		output.m_svPosition = mul(g_unis.m_viewProjMat, Vec4(localPos, 1.0));
	}
	else
	{
		// Skip this instance by making the vertex degenerate
		output.m_svPosition = 0.0f;
	}

	return output;
}
#pragma anki technique_end vert

#pragma anki technique_start frag
#include <AnKi/Shaders/ImportanceSampling.hlsl>

// NOTE: Don't eliminate the binding because it confuses the descriptor set creation
#if DEPTH_FAIL_VISUALIZATION == 1
[[vk::binding(0)]] SamplerState g_nearestAnyClampSampler;
[[vk::binding(1)]] Texture2D g_depthRt;
#endif

Vec4 main(VertOut input) : SV_TARGET0
{
	ANKI_MAYBE_UNUSED(input);

	// Check if we should skip the frag
#if DEPTH_FAIL_VISUALIZATION == 1
	Vec2 texSize;
	g_depthRt.GetDimensions(texSize.x, texSize.y);
	const Vec2 uv = input.m_svPosition.xy / texSize;
	const F32 depthRef = g_depthRt.SampleLevel(g_nearestAnyClampSampler, uv, 0.0).r;
	const Bool depthTestFailed = input.m_svPosition.z >= depthRef;
	if(depthTestFailed)
	{
		return g_unis.m_color * 0.5;
	}
#endif

	// Write the color
	return g_unis.m_color;
}
#pragma anki technique_end frag
