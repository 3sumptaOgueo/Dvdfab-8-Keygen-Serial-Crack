// Copyright (C) 2009-2023, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki mutator ANKI_VELOCITY 0 1
#pragma anki mutator ANKI_BONES 0 1
#pragma anki mutator DIFFUSE_TEX 0 1
#pragma anki mutator SPECULAR_TEX 0 1
#pragma anki mutator ROUGHNESS_METALNESS_TEX 0 1
#pragma anki mutator NORMAL_TEX 0 1
#pragma anki mutator PARALLAX 0 1
#pragma anki mutator EMISSIVE_TEX 0 1
#pragma anki mutator ALPHA_TEST 0 1

#pragma anki skip_mutation ALPHA_TEST 1 DIFFUSE_TEX 0

#include <AnKi/Shaders/Include/MaterialTypes.h>
#include <AnKi/Shaders/Include/GpuSceneFunctions.h>
#include <AnKi/Shaders/PackFunctions.hlsl>
#include <AnKi/Shaders/Functions.hlsl>
#include <AnKi/Shaders/MaterialShadersCommon.hlsl>
#include <AnKi/Shaders/RtShadows.hlsl>

// Define a few things to avoid compilation errors
#if defined(ANKI_TECHNIQUE_RtShadows) && defined(ANKI_CLOSEST_HIT_SHADER)
#	define ALPHA_TEST 0
#	define DIFFUSE_TEX 0
#	define ANKI_VELOCITY 0
#	define ANKI_BONES 0
#	define PARALLAX 0
#endif

#if defined(ANKI_TECHNIQUE_RtShadows) && defined(ANKI_ANY_HIT_SHADER)
#	define ANKI_VELOCITY 0
#	define ANKI_BONES 0
#	define PARALLAX 0
#endif

#if defined(ANKI_TASK_SHADER)
#	define ALPHA_TEST 0
#	define DIFFUSE_TEX 0
#	define ANKI_VELOCITY 0
#	define ANKI_BONES 0
#	define PARALLAX 0
#endif

// General defines
#define GBUFFER defined(ANKI_TECHNIQUE_GBuffer)
#define REALLY_ALPHA_TEST (ALPHA_TEST && DIFFUSE_TEX)
#define UVS (GBUFFER || REALLY_ALPHA_TEST)
#define REALLY_VELOCITY ((ANKI_VELOCITY || ANKI_BONES) && GBUFFER)
#define REALLY_USING_PARALLAX (PARALLAX == 1 && GBUFFER && ALPHA_TEST == 0)

#define VISUALIZE_MESHLETS (0 && GBUFFER)
#define MESHLET_BACKFACE_CULLING 0
#define MESHLET_OUTSIDE_OF_SCREEN_CULLING 1
#define MESHLET_NO_SAMPLING_POINT_CULLING 1
#define MESHLET_HZB_CULLING 1
#define PRIMITIVE_BACKFACE_CULLING 1
#define PRIMITIVE_NO_SAMPLING_POINTS_CULLING 1
#define PRIMITIVE_ANY_CULLING (PRIMITIVE_BACKFACE_CULLING || PRIMITIVE_NO_SAMPLING_POINTS_CULLING)

#pragma anki struct AnKiLocalConstants

#pragma anki member U32 m_diffuseTex
#pragma anki member U32 m_normalTex
#pragma anki member U32 m_roughnessMetalnessTex
#pragma anki member U32 m_specularTex
#pragma anki member U32 m_emissiveTex
#pragma anki member U32 m_heightTex

#pragma anki member RVec4 m_diffuseScale
#pragma anki member RF32 m_roughnessScale
#pragma anki member RF32 m_metalnessScale
#pragma anki member RVec3 m_specularScale
#pragma anki member RVec3 m_emissionScale
#pragma anki member RF32 m_heightmapScale
#pragma anki member RF32 m_subsurface

#pragma anki struct_end

struct VertIn
{
	U32 m_svVertexId : SV_VERTEXID;
	[[vk::location(0)]] UVec4 m_gpuSceneRenderable : RENDERABLE;
};

struct VertOut
{
	Vec4 m_svPosition : SV_POSITION;

#if UVS
	Vec2 m_uv : TEXCOORD;
#endif

#if REALLY_VELOCITY
	Vec3 m_prevClipXyw : PREV_CLIP;
	Vec3 m_crntClipXyw : CRNT_CLIP;
#endif

#if GBUFFER
	Vec3 m_worldPos : WORLD_POS;
	RVec3 m_normal : NORMAL;
#endif

	nointerpolation U32 m_constantsOffset : UNIS_OFFSET;

#if VISUALIZE_MESHLETS
	nointerpolation U32 m_meshletIndex : MESHLET_INDEX;
#endif
};

struct FragOut
{
	Vec4 m_color0 : SV_TARGET0;
	Vec4 m_color1 : SV_TARGET1;
	Vec4 m_color2 : SV_TARGET2;
	Vec2 m_color3 : SV_TARGET3;
};

struct TaskOut
{
	U32 m_firstMeshletIndex;
	U32 m_visibleMeshletsRelativeIndices[kMeshletGroupSize / sizeof(U32)];
	U32 m_worldTransformsOffset;
	U32 m_constantsOffset;
	U32 m_boneTransformsOrParticleEmitterOffset;
	Vec3 m_positionTranslation;
	F32 m_positionScale;
};

struct MeshPerPrimitiveOut
{
	I32 m_cullPrimitive : SV_CULLPRIMITIVE; // TODO: Make it Bool when https://github.com/microsoft/DirectXShaderCompiler/issues/6042 is fixed
};

struct Mat3x4_2
{
	Mat3x4 m_a;
	Mat3x4 m_b;
};

#if ANKI_BONES
Mat3x4_2 loadBoneTransforms(UnpackedMeshVertex vert, U32 boneTransformsOffset, U32 index)
{
	const U32 boneIdx = vert.m_boneIndices[index];
	U32 byteOffset = boneTransformsOffset;
	byteOffset += boneIdx * sizeof(Mat3x4) * 2;
	return g_gpuScene.Load<Mat3x4_2>(byteOffset);
}

void skinning(UnpackedMeshVertex vert, U32 boneTransformsOffset, inout Vec3 pos, inout Vec3 prevPos, inout RVec3 normal)
{
	Mat3x4_2 mats = loadBoneTransforms(vert, boneTransformsOffset, 0);

	Mat3x4 skinMat = mats.m_a * vert.m_boneWeights[0];
	Mat3x4 prevSkinMat = mats.m_b * vert.m_boneWeights[0];

	[unroll] for(U32 i = 1u; i < 4u; ++i)
	{
		mats = loadBoneTransforms(vert, boneTransformsOffset, i);

		skinMat = skinMat + mats.m_a * vert.m_boneWeights[i];
		prevSkinMat = prevSkinMat + mats.m_b * vert.m_boneWeights[i];
	}

#	if GBUFFER
	prevPos = mul(prevSkinMat, Vec4(pos, 1.0)).xyz;
	normal = mul(skinMat, Vec4(normal, 0.0)).xyz;
#	endif
	ANKI_MAYBE_UNUSED(prevPos);
	ANKI_MAYBE_UNUSED(normal);

	pos = mul(skinMat, Vec4(pos, 1.0)).xyz;
}
#endif

#if REALLY_VELOCITY
void velocity(Mat3x4 worldTransform, Mat3x4 prevWorldTransform, Vec3 prevLocalPos, inout VertOut output)
{
	ANKI_MAYBE_UNUSED(prevWorldTransform);
	ANKI_MAYBE_UNUSED(worldTransform);

#	if ANKI_VELOCITY
	// Object is also moving
	const Mat3x4 trf = prevWorldTransform;
#	else
	// Object is a skin that is not moving
	const Mat3x4 trf = worldTransform;
#	endif

	Vec4 v4 = Vec4(mul(trf, Vec4(prevLocalPos, 1.0)), 1.0);
	v4 = mul(g_globalConstants.m_previousViewProjectionMatrix, v4);

	output.m_prevClipXyw = v4.xyw;
	output.m_crntClipXyw = output.m_svPosition.xyw;
}
#endif

// ===========================================================================
// Vert                                                                      =
// ===========================================================================
#if defined(ANKI_VERTEX_SHADER)
VertOut main(VertIn input)
{
	VertOut output;

	const GpuSceneRenderableVertex renderable = unpackGpuSceneRenderableVertex(input.m_gpuSceneRenderable);
	const GpuSceneMeshLod mesh = g_gpuScene.Load<GpuSceneMeshLod>(renderable.m_meshLodOffset);
	UnpackedMeshVertex vert = loadVertex(mesh, input.m_svVertexId, ANKI_BONES);

	const Mat3x4 worldTransform = g_gpuScene.Load<Mat3x4>(renderable.m_worldTransformsOffset);
	const Mat3x4 prevWorldTransform = g_gpuScene.Load<Mat3x4>(renderable.m_worldTransformsOffset + sizeof(Mat3x4));
	ANKI_MAYBE_UNUSED(prevWorldTransform);

#	if UVS
	output.m_uv = vert.m_uv;
#	endif
	Vec3 prevPos = vert.m_position;
	ANKI_MAYBE_UNUSED(prevPos);
	output.m_constantsOffset = renderable.m_constantsOffset;

	// Do stuff
#	if ANKI_BONES
	skinning(vert, renderable.m_boneTransformsOrParticleEmitterOffset, vert.m_position, prevPos, vert.m_normal);
#	endif

	const Vec3 worldPos = mul(worldTransform, Vec4(vert.m_position, 1.0));
	output.m_svPosition = mul(g_globalConstants.m_viewProjectionMatrix, Vec4(worldPos, 1.0));

#	if GBUFFER
	output.m_worldPos = worldPos;
	output.m_normal = mul(worldTransform, Vec4(vert.m_normal, 0.0));
#	endif

#	if REALLY_VELOCITY
	velocity(worldTransform, prevWorldTransform, prevPos, output);
#	endif

	return output;
};
#endif // defined(ANKI_VERTEX_SHADER)

// ===========================================================================
// Task                                                                      =
// ===========================================================================
#if defined(ANKI_TASK_SHADER)
groupshared TaskOut s_payload;
groupshared U32 s_visibleMeshletCount;

struct FirstPayload
{
	UVec4 m_val;
};

[[vk::push_constant]] ConstantBuffer<FirstPayload> g_firstPayload;

[numthreads(ANKI_TASK_SHADER_THREADGROUP_SIZE, 1, 1)] void main(U32 svGroupId : SV_GROUPID, U32 svGroupIndex : SV_GROUPINDEX)
{
	const GpuSceneTaskShaderPayload inPayload = g_taskShaderPayloads[g_firstPayload.m_val.x + svGroupId];

	const U32 lod = inPayload.m_lod_2bit_renderableIdx_21bit_meshletGroup_9bit >> 30u;
	const U32 renderableIdx = (inPayload.m_lod_2bit_renderableIdx_21bit_meshletGroup_9bit >> 9u) & ((1u << 21u) - 1u);
	const U32 meshletGroup = inPayload.m_lod_2bit_renderableIdx_21bit_meshletGroup_9bit & ((1u << 9u) - 1u);

	const GpuSceneRenderable renderable = g_renderables[renderableIdx];
	const GpuSceneMeshLod meshLod = g_gpuScene.Load<GpuSceneMeshLod>(renderable.m_meshLodsOffset + sizeof(GpuSceneMeshLod) * lod);
	U32 firstMeshlet = meshletGroup * kMeshletGroupSize;
	const U32 meshletCount = min(kMeshletGroupSize, meshLod.m_meshletCount - firstMeshlet);
	firstMeshlet += meshLod.m_firstMeshlet;

	if(svGroupIndex == 0u)
	{
		s_payload.m_firstMeshletIndex = firstMeshlet;
		s_payload.m_worldTransformsOffset = renderable.m_worldTransformsOffset;
		s_payload.m_constantsOffset = renderable.m_constantsOffset;
		s_payload.m_boneTransformsOrParticleEmitterOffset = renderable.m_boneTransformsOffset;
		s_payload.m_positionScale = meshLod.m_positionScale;
		s_payload.m_positionTranslation = meshLod.m_positionTranslation;

		s_visibleMeshletCount = 0;

		[unroll] for(U32 i = 0; i < kMeshletGroupSize / sizeof(U32); ++i)
		{
			s_payload.m_visibleMeshletsRelativeIndices[i] = 0u;
		}
	}

	GroupMemoryBarrierWithGroupSync();

	if(svGroupIndex < meshletCount)
	{
		Bool cull = false;

		const Meshlet meshlet = g_meshlets[firstMeshlet + svGroupIndex];
		const Mat3x4 worldTransform = g_gpuScene.Load<Mat3x4>(renderable.m_worldTransformsOffset);

#	if MESHLET_BACKFACE_CULLING
		cull = cullBackfaceMeshlet(meshlet, worldTransform, g_globalConstants.m_cameraTransform.getTranslationPart());
#	endif

		const Mat4 wordTransform4 = {worldTransform.m_row0, worldTransform.m_row1, worldTransform.m_row2, Vec4(0.0f, 0.0f, 0.0f, 1.0f)};
		const Mat4 mvp = mul(g_globalConstants.m_viewProjectionMatrix, wordTransform4);

		Vec2 minNdc, maxNdc;
		F32 aabbMinDepth;
		projectAabb(meshlet.m_aabbMin, meshlet.m_aabbMax, mvp, minNdc, maxNdc, aabbMinDepth);

#	if MESHLET_OUTSIDE_OF_SCREEN_CULLING
		// Outside of the screen
		cull = cull || (any(minNdc > 1.0f) || any(maxNdc < -1.0f));
#	endif

#	if MESHLET_NO_SAMPLING_POINT_CULLING
		// Sampling points test
		const Vec2 windowCoordsMin = ndcToUv(minNdc) * g_globalConstants.m_viewport.zw;
		const Vec2 windowCoordsMax = ndcToUv(maxNdc) * g_globalConstants.m_viewport.zw;
		cull = cull || any(round(windowCoordsMin) == round(windowCoordsMax));
#	endif

#	if MESHLET_HZB_CULLING
		cull = cull || (g_globalConstants.m_enableHzbTesting == 1u && cullHzb(minNdc, maxNdc, aabbMinDepth, g_hzbTexture, g_nearestClampSampler));
#	endif

		if(!cull)
		{
			U32 idx;
			InterlockedAdd(s_visibleMeshletCount, 1u, idx);

			const U32 groupIdx = idx / 4u;
			const U32 localIdx = idx % 4u;

			const U32 mask = svGroupIndex << (localIdx * 8u);
			InterlockedOr(s_payload.m_visibleMeshletsRelativeIndices[groupIdx], mask);
		}
	}

	DispatchMesh(s_visibleMeshletCount, 1, 1, s_payload);
}
#endif

// ===========================================================================
// Mesh                                                                      =
// ===========================================================================
#if defined(ANKI_MESH_SHADER)
#	if PRIMITIVE_ANY_CULLING
groupshared Vec2 s_windowCoords[kMaxVerticesPerMeshlet];
groupshared F32 s_clipW[kMaxVerticesPerMeshlet];
#	endif

[numthreads(ANKI_MESH_SHADER_THREADGROUP_SIZE, 1, 1)] [outputtopology("triangle")] void
main(in payload TaskOut payload, out vertices VertOut verts[kMaxVerticesPerMeshlet], out indices UVec3 indices[kMaxPrimitivesPerMeshlet],
	 U32 svGroupId : SV_GROUPID, U32 svGroupIndex : SV_GROUPINDEX
#	if PRIMITIVE_ANY_CULLING
	 ,
	 out primitives MeshPerPrimitiveOut primitives[kMaxPrimitivesPerMeshlet]
#	endif
) {
	const U32 groupIdx = svGroupId / 4u;
	const U32 localIdx = svGroupId % 4u;
	const U32 relativeMeshletIdx = (payload.m_visibleMeshletsRelativeIndices[groupIdx] >> (localIdx * 8u)) & 0xFFu;

	const Meshlet meshlet = g_meshlets[payload.m_firstMeshletIndex + relativeMeshletIdx];
	const U32 primCount = meshlet.m_primitiveCount_R16_Uint_vertexCount_R16_Uint >> 16u;
	const U32 vertCount = meshlet.m_primitiveCount_R16_Uint_vertexCount_R16_Uint & 0xFFFFu;

	SetMeshOutputCounts(vertCount, primCount);

	// Write the verts
	const U32 vertLoopCount = kMaxVerticesPerMeshlet / ANKI_MESH_SHADER_THREADGROUP_SIZE;
	[unroll] for(U32 l = 0; l < vertLoopCount; ++l)
	{
		const U32 idx = l * ANKI_MESH_SHADER_THREADGROUP_SIZE + svGroupIndex;

		if(idx < vertCount)
		{
			VertOut output;

			UnpackedMeshVertex vert = loadVertex(meshlet, idx, ANKI_BONES, payload.m_positionScale, payload.m_positionTranslation);

			const Mat3x4 worldTransform = g_gpuScene.Load<Mat3x4>(payload.m_worldTransformsOffset);
			const Mat3x4 prevWorldTransform = g_gpuScene.Load<Mat3x4>(payload.m_worldTransformsOffset + sizeof(Mat3x4));
			ANKI_MAYBE_UNUSED(prevWorldTransform);

#	if UVS
			output.m_uv = vert.m_uv;
#	endif
			Vec3 prevPos = vert.m_position;
			ANKI_MAYBE_UNUSED(prevPos);
			output.m_constantsOffset = payload.m_constantsOffset;

			// Do stuff
#	if ANKI_BONES
			skinning(vert, payload.m_boneTransformsOrParticleEmitterOffset, vert.m_position, prevPos, vert.m_normal);
#	endif

			const Vec3 worldPos = mul(worldTransform, Vec4(vert.m_position, 1.0));
			output.m_svPosition = mul(g_globalConstants.m_viewProjectionMatrix, Vec4(worldPos, 1.0f));
#	if PRIMITIVE_ANY_CULLING
			s_windowCoords[idx] = ndcToUv(output.m_svPosition.xy / output.m_svPosition.w) * g_globalConstants.m_viewport.zw;
			s_clipW[idx] = output.m_svPosition.w;
#	endif

#	if GBUFFER
			output.m_worldPos = worldPos;
			output.m_normal = mul(worldTransform, Vec4(vert.m_normal, 0.0));
#	endif

#	if REALLY_VELOCITY
			velocity(worldTransform, prevWorldTransform, prevPos, output);
#	endif

#	if VISUALIZE_MESHLETS
			output.m_meshletIndex = relativeMeshletIdx;
#	endif

			verts[idx] = output;
		}
	}

#	if PRIMITIVE_ANY_CULLING
	GroupMemoryBarrierWithGroupSync();
#	endif

	// Write the indices
	const U32 primLoopCount = kMaxPrimitivesPerMeshlet / ANKI_MESH_SHADER_THREADGROUP_SIZE;
	[unroll] for(U32 l = 0; l < primLoopCount; ++l)
	{
		const U32 idx = l * ANKI_MESH_SHADER_THREADGROUP_SIZE + svGroupIndex;

		if(idx < primCount)
		{
			const UVec3 prim = g_unifiedGeom_R8G8B8A8_Uint[meshlet.m_firstPrimitive + idx].xyz;
			indices[idx] = prim;

#	if PRIMITIVE_ANY_CULLING
			Bool cull = false;

			const Vec2 a = s_windowCoords[prim.x];
			const Vec2 b = s_windowCoords[prim.y];
			const Vec2 c = s_windowCoords[prim.z];
#	endif

#	if PRIMITIVE_BACKFACE_CULLING
			const Vec2 eb = c - a;
			const Vec2 ec = b - a;

			cull = cull || (eb.x * ec.y >= eb.y * ec.x);
#	endif

#	if PRIMITIVE_NO_SAMPLING_POINTS_CULLING
			const Vec2 windowCoordsMin = min3(a, b, c);
			const Vec2 windowCoordsMax = max3(a, b, c);

			cull = cull || any(round(windowCoordsMin) == round(windowCoordsMax));
#	endif

#	if PRIMITIVE_ANY_CULLING
			// The computations above are only valid if all vertices are in front of perspective plane
			cull = cull && min3(s_clipW[prim.x], s_clipW[prim.y], s_clipW[prim.z]) > 0.0f;

			primitives[idx].m_cullPrimitive = cull;
#	endif
		}
	}
}
#endif // defined(ANKI_MESH_SHADER)

// ===========================================================================
// Frag                                                                      =
// ===========================================================================
#if defined(ANKI_FRAGMENT_SHADER)
#	if !GBUFFER
void main(VertOut input)
{
	ANKI_MAYBE_UNUSED(input);
#		if REALLY_ALPHA_TEST
	const AnKiLocalConstants localConstants = loadAnKiLocalConstants(g_gpuScene, input.m_constantsOffset);
	const RVec4 diffColorA = g_bindlessTextures2dF32[localConstants.m_diffuseTex].Sample(g_globalSampler, input.m_uv);
	if(diffColorA.a * localConstants.m_diffuseScale.a == 0.0f)
	{
		discard;
	}
#		endif
}

#	else // GBUFFER

FragOut main(VertOut input)
{
	const AnKiLocalConstants localConstants = loadAnKiLocalConstants(g_gpuScene, input.m_constantsOffset);

#		if REALLY_USING_PARALLAX
	// TODO
	const Vec2 uv = input.m_uv;
#		else
	const Vec2 uv = input.m_uv;
#		endif
	ANKI_MAYBE_UNUSED(uv);

#		if DIFFUSE_TEX
#			if REALLY_ALPHA_TEST
	const RVec4 diffColorA = g_bindlessTextures2dF32[localConstants.m_diffuseTex].Sample(g_globalSampler, uv) * localConstants.m_diffuseScale;
	if(diffColorA.a == 0.0f)
	{
		discard;
	}
	const RVec3 diffColor = diffColorA.rgb;
#			else
	const RVec3 diffColor = g_bindlessTextures2dF32[localConstants.m_diffuseTex].Sample(g_globalSampler, uv).rgb;
#			endif
#		else
	const RVec3 diffColor = localConstants.m_diffuseScale;
#		endif

#		if SPECULAR_TEX
	const RVec3 specColor = g_bindlessTextures2dF32[localConstants.m_specularTex].Sample(g_globalSampler, uv).rgb * localConstants.m_specularScale;
#		else
	const RVec3 specColor = localConstants.m_specularScale;
#		endif

#		if ROUGHNESS_METALNESS_TEX
	const RVec3 comp = g_bindlessTextures2dF32[localConstants.m_roughnessMetalnessTex].Sample(g_globalSampler, uv).rgb;
	const RF32 roughness = comp.g * localConstants.m_roughnessScale;
	const RF32 metallic = comp.b * localConstants.m_metalnessScale;
#		else
	const RF32 roughness = localConstants.m_roughnessScale;
	const RF32 metallic = localConstants.m_metalnessScale;
#		endif

#		if NORMAL_TEX
	const RVec3 nAtTangentspace = normalize((g_bindlessTextures2dF32[localConstants.m_normalTex].Sample(g_globalSampler, uv).rgb - 0.5) * 2.0);
	const Vec3 viewDir = normalize(g_globalConstants.m_cameraTransform.getTranslationPart() - input.m_worldPos);
	const RVec3 normal = perturbNormal(nAtTangentspace, viewDir, uv, normalize(input.m_normal));
#		else
	const RVec3 normal = normalize(input.m_normal);
#		endif

#		if EMISSIVE_TEX
	const RVec3 emission = g_bindlessTextures2dF32[localConstants.m_emissiveTex].Sample(g_globalSampler, uv).rgb * localConstants.m_emissionScale;
#		else
	const RVec3 emission = localConstants.m_emissionScale;
#		endif

#		if ANKI_VELOCITY || ANKI_BONES
	const Vec2 prevNdc = input.m_prevClipXyw.xy / input.m_prevClipXyw.z;
	const Vec2 crntNdc = input.m_crntClipXyw.xy / input.m_crntClipXyw.z;

	// It's ndcToUv(prevNdc) - ndcToUv(crntNdc) or:
	const Vec2 velocity = (prevNdc - crntNdc) * 0.5;
#		else
	const Vec2 velocity = Vec2(1.0, 1.0);
#		endif

	GbufferInfo g;
	g.m_diffuse = diffColor;
	g.m_normal = normal;
	g.m_f0 = specColor;
	g.m_roughness = roughness;
	g.m_subsurface = localConstants.m_subsurface;
	g.m_emission = emission;
	g.m_metallic = metallic;
	g.m_velocity = velocity;

#		if VISUALIZE_MESHLETS
	const U32 meshletIdx = input.m_meshletIndex % 6u;
	switch(meshletIdx)
	{
	case 0:
		g.m_diffuse = Vec3(1.0f, 0.0f, 0.0f);
		break;
	case 1:
		g.m_diffuse = Vec3(0.0f, 1.0f, 0.0f);
		break;
	case 2:
		g.m_diffuse = Vec3(0.0f, 0.0f, 1.0f);
		break;
	case 3:
		g.m_diffuse = Vec3(1.0f, 1.0f, 0.0f);
		break;
	case 4:
		g.m_diffuse = Vec3(1.0f, 0.0f, 1.0f);
		break;
	case 5:
		g.m_diffuse = Vec3(0.0f, 1.0f, 1.0f);
		break;
	}
#		endif

	FragOut output;
	packGBuffer(g, output.m_color0, output.m_color1, output.m_color2, output.m_color3);
	return output;
}
#	endif
#endif // defined(ANKI_FRAGMENT_SHADER)

// ===========================================================================
// RT Shadows                                                                =
// ===========================================================================
#if defined(ANKI_ANY_HIT_SHADER)

#	if REALLY_ALPHA_TEST
[[vk::shader_record_ext]] ConstantBuffer<GpuSceneRenderableVertex> g_gpuSceneRenderable;
#	endif

[shader("anyhit")] void main(inout RayPayload payload, in Barycentrics barycentrics)
{
	ANKI_MAYBE_UNUSED(payload);
	ANKI_MAYBE_UNUSED(barycentrics);

#	if REALLY_ALPHA_TEST
	payload.m_shadowFactor = 1.0;

	const Vec3 bary = Vec3(1.0f - barycentrics.m_value.x - barycentrics.m_value.y, barycentrics.m_value.x, barycentrics.m_value.y);

	const GpuSceneMeshLod mesh = g_gpuScene.Load<GpuSceneMeshLod>(g_gpuSceneRenderable.m_meshLodOffset);

	const U32 idx0 = g_unifiedGeom_R16_Uint[mesh.m_firstIndex + PrimitiveIndex() * 3 + 0];
	const U32 idx1 = g_unifiedGeom_R16_Uint[mesh.m_firstIndex + PrimitiveIndex() * 3 + 1];
	const U32 idx2 = g_unifiedGeom_R16_Uint[mesh.m_firstIndex + PrimitiveIndex() * 3 + 2];

	const UnpackedMeshVertex vert0 = loadVertex(mesh, idx0, false);
	const UnpackedMeshVertex vert1 = loadVertex(mesh, idx1, false);
	const UnpackedMeshVertex vert2 = loadVertex(mesh, idx2, false);

	const Vec2 uv = vert0.m_uv * bary.x + vert1.m_uv * bary.y + vert2.m_uv * bary.z;

	const AnKiLocalConstants localConstants = loadAnKiLocalConstants(g_gpuScene, g_gpuSceneRenderable.m_constantsOffset);
	const RVec4 diffColorA =
		g_bindlessTextures2dF32[localConstants.m_diffuseTex].SampleLevel(g_globalSampler, uv, 0.0) * localConstants.m_diffuseScale;

	if(diffColorA.a < 1.0)
	{
		payload.m_shadowFactor = 0.0;
		AcceptHitAndEndSearch();
	}
	else
	{
		IgnoreHit();
	}
#	else
	payload.m_shadowFactor = 0.0;
	AcceptHitAndEndSearch();
#	endif
}
#endif // defined(ANKI_ANY_HIT_SHADER)

#if defined(ANKI_CLOSEST_HIT_SHADER)
[shader("closesthit")] void main(inout RayPayload payload, in Barycentrics barycentrics)
{
	ANKI_MAYBE_UNUSED(payload);
	ANKI_MAYBE_UNUSED(barycentrics);
}
#endif // defined(ANKI_CLOSEST_HIT_SHADER)

// ===========================================================================
// Define the techniques                                                     =
// ===========================================================================
#pragma anki technique_start vert GBuffer uses_mutators ANKI_VELOCITY ANKI_BONES DIFFUSE_TEX PARALLAX ALPHA_TEST
#pragma anki technique_end vert GBuffer

#pragma anki technique_start task GBuffer uses_mutators
#pragma anki technique_end task GBuffer

#pragma anki technique_start mesh GBuffer uses_mutators ANKI_VELOCITY ANKI_BONES DIFFUSE_TEX PARALLAX ALPHA_TEST
#pragma anki technique_end mesh GBuffer

#pragma anki technique_start frag GBuffer
#pragma anki technique_end frag GBuffer

#pragma anki technique_start vert Shadows uses_mutators ANKI_VELOCITY ANKI_BONES DIFFUSE_TEX PARALLAX ALPHA_TEST
#pragma anki technique_end vert Shadows

#pragma anki technique_start task Shadows uses_mutators
#pragma anki technique_end task Shadows

#pragma anki technique_start mesh Shadows uses_mutators ANKI_VELOCITY ANKI_BONES DIFFUSE_TEX PARALLAX ALPHA_TEST
#pragma anki technique_end mesh Shadows

#pragma anki technique_start frag Shadows
#pragma anki technique_end frag Shadows

#pragma anki technique_start ahit RtShadows uses_mutators ALPHA_TEST DIFFUSE_TEX
#pragma anki technique_end ahit RtShadows

#pragma anki technique_start chit RtShadows uses_mutators
#pragma anki technique_end chit RtShadows
